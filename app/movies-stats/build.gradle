ext {
    awsLambdaJavaCoreVersion = "1.2.0"
    awsLambdaJavaEventsVersion = "2.2.7"
    awsLambdaJavaLog4j2Version = "1.1.0"
    awsXrayJavaSdkVersion = "2.4.0"
    awsJavaSdkVersion = "1.11.717"
    gradleVersion = "6.1.1"
    jacksonVersion = "2.10.2"
    log4jVersion = "2.13.0"
}

subprojects {
    apply plugin: "java"
    apply plugin: "idea"

    group = "de.mbe.tutorials.aws.serverless.moviesstats"
    version = "1.0.0"

    java {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }

    repositories {
        mavenCentral()
    }

    test {
        useJUnitPlatform()
        testLogging {
            events "passed", "skipped", "failed"
        }
    }
}

configure(subprojects.findAll {it.name != "models"}) {
    dependencies {
        implementation project(":models")

        implementation "com.amazonaws:aws-lambda-java-core:${awsLambdaJavaCoreVersion}"
        implementation "com.amazonaws:aws-lambda-java-events:${awsLambdaJavaEventsVersion}"
        implementation "com.amazonaws:aws-lambda-java-log4j2:${awsLambdaJavaLog4j2Version}"
        implementation "org.apache.logging.log4j:log4j-core:${log4jVersion}"
        implementation "org.apache.logging.log4j:log4j-api:${log4jVersion}"

        implementation platform("com.amazonaws:aws-xray-recorder-sdk-bom:${awsXrayJavaSdkVersion}")
        implementation "com.amazonaws:aws-xray-recorder-sdk-core"
        implementation "com.amazonaws:aws-xray-recorder-sdk-aws-sdk"

        implementation platform("com.amazonaws:aws-java-sdk-bom:${awsJavaSdkVersion}")
    }

    task buildLayerZip(type: Zip) {
        into("java/lib") {
            from configurations.runtimeClasspath
        }
        archiveFileName = "${project.name}-layer.zip"
    }

    task buildZip(type: Zip) {
        from compileJava
        from processResources
        archiveFileName = "${project.name}.zip"
    }

    build.dependsOn buildLayerZip
    build.dependsOn buildZip
}

wrapper {
    gradleVersion = "${gradleVersion}"
    distributionType = Wrapper.DistributionType.ALL
}